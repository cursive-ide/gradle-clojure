/*
 * Copyright 2016 Colin Fleming
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  ext.kotlin_version = '1.0.3'

  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.5"
  }
}

group 'com.cursive-ide'
version '1.1.0'
ext.pluginId = 'com.cursive-ide.clojure'
ext.pluginImplementationClass = 'cursive.ClojurePlugin'

apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: "java-gradle-plugin"

project.afterEvaluate {
  // Ugh, see https://discuss.gradle.org/t/kotlin-groovy-and-java-compilation/14903/9
  compileGroovy.dependsOn = compileGroovy.taskDependencies.values - 'compileJava'
  compileKotlin.dependsOn compileGroovy
  compileKotlin.classpath += files(compileGroovy.destinationDir)
  classes.dependsOn compileKotlin
}

repositories {
  jcenter()
  mavenLocal()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  testCompile gradleTestKit()
  testCompile "junit:junit:4.12"
  testCompile 'net.wuerl.kotlin:assertj-core-kotlin:0.1.3'
}

gradlePlugin {
  plugins {
    clojurePlugin {
      id = pluginId
      implementationClass = pluginImplementationClass
    }
  }
}

pluginBundle {
  website = 'https://plugins.gradle.org/plugin/com.cursive-ide.clojure'
  vcsUrl = 'https://github.com/cursive-ide/gradle-clojure'
  description = 'Compiles Clojure code and runs tests'
  tags = ['clojure']

  plugins {
    clojurePlugin {
      id = pluginId
      displayName = 'Gradle Clojure plugin'
    }
  }
}
